Name "Tagaini Jisho ${VERSION}"
OutFile "install.exe"
SetCompressor /SOLID "lzma"

InstallDir "$PROGRAMFILES\Tagaini Jisho"
RequestExecutionLevel admin
LicenseData "COPYING"
Page license
Page directory
Page instfiles
UninstPage uninstConfirm
UninstPage instfiles

Section "Tagaini Jisho"
IfFileExists "$LOCALAPPDATA\VirtualStore\Program Files\Tagaini Jisho\kanjidic2.db" VSTOREKANJIDICEXISTS VSTOREKANJIDICEND
VSTOREKANJIDICEXISTS:
Delete "$LOCALAPPDATA\VirtualStore\Program Files\Tagaini Jisho\jmdict.db"
Delete "$LOCALAPPDATA\VirtualStore\Program Files\Tagaini Jisho\kanjidic2.db"
RMDir "$LOCALAPPDATA\VirtualStore\Program Files\Tagaini Jisho"
VSTOREKANJIDICEND:

SetOutPath "$INSTDIR"
File "${BUILDDIR}/src/gui/tagainijisho.exe"
File /oname=jmdict.db "${DBDIR}/jmdict-${LANG}.db"
SetFileAttributes "jmdict.db" READONLY
File /oname=kanjidic2.db "${DBDIR}/kanjidic2-${LANG}.db"
SetFileAttributes "kanjidic2.db" READONLY
File /oname=export_template.html "src/gui/export_template.html"
File "${QTPATH}/bin/QtCore4.dll"
File "${QTPATH}/bin/QtGui4.dll"
File "${QTPATH}/bin/QtSql4.dll"
File "${QTPATH}/bin/QtNetWork4.dll"
File "${MINGWDLLPATH}/mingwm10.dll"
File "${MINGWDLLPATH}/libgcc_s_dw2-1.dll"
File /oname=qt.conf "qt.conf.windows"
SetOutPath "$INSTDIR\i18n"
File "win32-translations/qt_fr.qm"
File "win32-translations/qt_de.qm"
File "win32-translations/qt_es.qm"
File "win32-translations/qt_ru.qm"
File "win32-translations/qt_nl.qm"
File "i18n/tagainijisho_fr.qm"
File "i18n/tagainijisho_de.qm"
File "i18n/tagainijisho_es.qm"
File "i18n/tagainijisho_ru.qm"
File "i18n/tagainijisho_nl.qm"
SetOutPath "$INSTDIR\imageformats"
File "${QTPATH}/plugins/imageformats/qgif4.dll"
SetOutPath "$INSTDIR\doc"
File "doc/manual.html"
File /r "doc/images"
WriteUninstaller "$INSTDIR\uninstall.exe"
CreateDirectory "$SMPROGRAMS\Tagaini Jisho"
CreateShortCut "$SMPROGRAMS\Tagaini Jisho\Tagaini Jisho.lnk" "$INSTDIR\tagainijisho.exe"
CreateShortCut "$SMPROGRAMS\Tagaini Jisho\Uninstall.lnk" "$INSTDIR\uninstall.exe"
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tagaini Jisho" "DisplayName" "Tagaini Jisho"
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tagaini Jisho" "UninstallString" "$INSTDIR/uninstall.exe"
SectionEnd

Section "uninstall"
Delete "$SMPROGRAMS\Tagaini Jisho\Tagaini Jisho.lnk"
Delete "$SMPROGRAMS\Tagaini Jisho\Uninstall.lnk"
RMDir "$SMPROGRAMS\Tagaini Jisho"
RMDir /r "$INSTDIR\doc\images"
Delete "$INSTDIR\doc\manual.html"
RMDir "$INSTDIR\doc"
Delete "$INSTDIR\i18n\qt_nl.qm"
Delete "$INSTDIR\i18n\qt_ru.qm"
Delete "$INSTDIR\i18n\qt_es.qm"
Delete "$INSTDIR\i18n\qt_de.qm"
Delete "$INSTDIR\i18n\qt_fr.qm"
Delete "$INSTDIR\i18n\tagainijisho_fr.qm"
Delete "$INSTDIR\i18n\tagainijisho_de.qm"
Delete "$INSTDIR\i18n\tagainijisho_es.qm"
Delete "$INSTDIR\i18n\tagainijisho_ru.qm"
Delete "$INSTDIR\i18n\tagainijisho_nl.qm"
RMDir "$INSTDIR\i18n"
Delete "$INSTDIR\imageformats\qgif4.dll"
RMDir "$INSTDIR\imageformats"
Delete "$INSTDIR\QtCore4.dll"
Delete "$INSTDIR\QtGui4.dll"
Delete "$INSTDIR\QtSql4.dll"
Delete "$INSTDIR\QtNetwork4.dll"
Delete "$INSTDIR\qt.conf"
Delete "$INSTDIR\libgcc_s_dw2-1.dll"
Delete "$INSTDIR\mingwm10.dll"
Delete "$INSTDIR\export_template.html"
Delete "$INSTDIR\kanjidic2.db"
Delete "$INSTDIR\jmdict.db"
Delete "$INSTDIR\tagainijisho.exe"
Delete "$INSTDIR\uninstall.exe"
RMDir "$INSTDIR"
DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Tagaini Jisho"
SectionEnd

